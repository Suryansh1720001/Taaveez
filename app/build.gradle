plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp' // Replaced kotlin-kapt with KSP
    // Removed kotlin-android (redundant with org.jetbrains.kotlin.android)
    // Removed kotlin-android-extensions (deprecated)
}

android {
    namespace 'com.itssuryansh.taaveez'

    lint{
        baseline = file("lint-baseline.xml")
    }
    compileSdk 35

    defaultConfig {
        applicationId 'com.itssuryansh.taaveez'
        minSdk 19
        targetSdk 35
        versionCode 6
        versionName "v1.2.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true // Also shrink unused resources
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false // Keep debug builds fast
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11 // Updated for AGP 8.1+
        targetCompatibility JavaVersion.VERSION_11 // Updated for AGP 8.1+
    }

    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }

    kotlinOptions {
        jvmTarget = '11' // Updated to match Java 11
    }

    buildFeatures{
        viewBinding true
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:$rootProject.appCompatVersion"
    implementation "androidx.activity:activity-ktx:$rootProject.activityVersion"

    // Room components - using KSP instead of KAPT
    implementation "androidx.room:room-ktx:$rootProject.roomVersion"
    implementation 'com.google.android.play:app-update-ktx:2.1.0'
    ksp "androidx.room:room-compiler:$rootProject.roomVersion" // Changed from kapt to ksp
    androidTestImplementation "androidx.room:room-testing:$rootProject.roomVersion"

    // Lifecycle components
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$rootProject.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$rootProject.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$rootProject.lifecycleVersion"

    // Kotlin components
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$rootProject.kotlin_version"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$rootProject.coroutines"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$rootProject.coroutines"

    // UI
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraintLayoutVersion"
    implementation "com.google.android.material:material:$rootProject.materialVersion"

    // Testing
    testImplementation "junit:junit:$rootProject.junitVersion"
    androidTestImplementation "androidx.arch.core:core-testing:$rootProject.coreTestingVersion"
    androidTestImplementation ("androidx.test.espresso:espresso-core:$rootProject.espressoVersion") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation "androidx.test.ext:junit:$rootProject.androidxJunitVersion"

    //for gif
    implementation 'com.github.bumptech.glide:glide:4.15.1'

    // for activity open transition
    // implementation 'com.github.AtifSayings:Animatoo:1.0.1'

    implementation 'io.github.medyo:android-about-page:2.0.0'

    // for markdown language
    implementation 'com.github.AppIntro:AppIntro:6.2.0'
    implementation 'jp.wasabeef:richeditor-android:2.0.0'
    implementation "androidx.webkit:webkit:1.7.0"

    def multidex_version = "2.0.1"
    implementation "androidx.multidex:multidex:$multidex_version"
}